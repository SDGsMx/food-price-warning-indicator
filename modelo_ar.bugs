
model
{
  #Likelihood
  # Space eq
  for(i in 4:(n-r)){
    for(j in 1:m){
      P[i,j] ~ dnorm(mu[i,j], tau.p)
      mu[i,j] <- alpha0 + alpha1*f[i-1] + alpha2*f[i-2] + alpha3*f[i-3] + gamma[i] + beta[j]
    }
    gamma[i] ~ dnorm(gamma[i-1], tau.g)
  }
  #priors
  alpha0 ~ dnorm(0,0.001)
  alpha1 ~ dnorm(0,0.001)
  alpha2 ~ dnorm(0,0.001)
  alpha3 ~ dnorm(0,0.001)
  for(i in 1:3){
    gamma[i] ~ dnorm(0, 0.001)
  }
  for(j in 1:m){
    beta[j] ~ dnorm(0,0.001)
  }
  tau.p ~ dgamma(0.001,0.001)
  tau.g ~ dgamma(0.001,0.001)
  #prediccion 1
  for(i in 1:(n-3-r)){
    for(j in 1:m){
      yf[i,j] ~ dnorm(mu[i+3,j], tau.p)
    }
  }
  #prediccion 2
  for(i in 1:r){
    for(j in 1:m){
      yp[i,j] ~ dnorm(mup[i,j], tau.p)
      mup[i,j] <- alpha0 + alpha1*f[n+i-1] + alpha2*f[n+i-2] + alpha3*f[n+i-3] + gamma[n-r+i] + beta[j]
    }
    gamma[n-r+i] ~ dnorm(gamma[n-r+i-1], tau.g)
  }
}
